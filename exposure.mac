load ("decomp.mac")$

/*
  The attrit matrix for two exposure levels.
  q^2 to avoid sqrts latter.
  k1 = k0 k
  k2 = k0 k k
*/
X2: matrix(
  [1,0,k0,k0],
  [0,1,k*k0,k*k0],
  [(q^2)*k0,(q^2)*k0,1,0],
  [(q^2)*k*k0,(q^2)*k*k0,0,1])$

/* The attrit matrix for three exposure levels. */
X3: matrix(
  [1,0,0,k0,k0,k0],
  [0,1,0,k*k0,k*k0,k*k0],
  [0,0,1,k^2*k0,k^2*k0,k^2*k0],
  [(q^2)*k0,(q^2)*k0,(q^2)*k0,1,0,0],
  [(q^2)*k*k0,(q^2)*k*k0,(q^2)*k*k0,0,1,0],
  [(q^2)*k^2*k0,(q^2)*k^2*k0,(q^2)*k^2*k0,0,0,1])$

testVals: [q  = 1/3^2, a0 = 3, a1 = 5, a2 = 7, b0 = 11, b1 = 13, b2 = 17, k = 19, k0 = 23, n = 3]$

substVals (M) := apply (ev, cons (M, testVals))$

Y2: eigenDecompose (X2, n)$

Z2: columnvector ([a0, a1, b0, b1])$

testY2: is (substVals (Y2) = substVals (X2 ^^ n))$

massSumY2: expand (factor (
  block ([M],
    M: Y2 . Z2,
    M [1][1] + M [2][1] + M [3][1] + M [4][1])))$

massSimplY2:
  expand (factor (ev (
    (A * q * (L0 * (1 + q) + L1 * (1 - q)) +
     B     * (L0 * (1 + q) - L1 * (1 - q)))/
    (2 * q),
      A = a1 + a0,
      B = b1 + b0,
      L0 = (1 + q * k0 * (1 + k))^n,
      L1 = (1 - q * k0 * (1 + k))^n)))$

checkSimplY2: is (massSumY2 = massSimplY2)$

Y3: eigenDecompose (X3, n)$

Z3: columnvector ([a0, a1, a2, b0, b1, b2])$

testY3: is (substVals (Y3) = substVals (X3 ^^ n))$

massSumY3: expand (factor (
  block ([M],
    M: Y3 . Z3,
    M [1][1] + M [2][1] + M [3][1] + M [4][1] + M [5][1] + M [6][1])))$

/*
  clear pattern. total losses not impacted by exposure distrib.
*/
massSimplY3:
  expand (factor (ev (
    (A * q * (L0 * (1 + q) + L1 * (1 - q)) +
     B     * (L0 * (1 + q) - L1 * (1 - q)))/
    (2 * q),
      A = a2 + a1 + a0,
      B = b2 + b1 + b0,
      L0 = (1 + q * k0 * (1 + k + k^2))^n,
      L1 = (1 - q * k0 * (1 + k + k^2))^n)))$

checkSimplY3: is (massSumY3 = massSimplY3)$
